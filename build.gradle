plugins {
	id 'java-library'
	id 'eclipse'
	id 'distribution'
}

version = '2.0-beta'

repositories {
	jcenter()
}

dependencies {
	compileOnly fileTree("${cbHome}/tomcat/webapps/cb/WEB-INF/lib") { include '*.jar' }
	compileOnly fileTree("${cbHome}/tomcat/lib") { include '*.jar' }
	testImplementation fileTree("${cbHome}/tomcat/webapps/cb/WEB-INF/lib") { include '*.jar' }
	testImplementation fileTree("${cbHome}/tomcat/lib") { include '*.jar' }
	testImplementation  'org.testng:testng:6.14.3'
	testImplementation 'org.mockito:mockito-core:1.10.19'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/java"
			includes = ["**/*.zip"]
        }
    }
}

test {
	useTestNG()
}


distributions {
	main {
		contents {

			into("WEB-INF/lib") {
				from (project.configurations.runtimeClasspath)
				from jar
			}
			from("src/main/web")
		}
	}
	src {
		contents {
			from("/") {
				include("src/**")
				include("build.gradle")
				include("settings.gradle")
			}
		}
	}
}

distZip {
	includeEmptyDirs = false
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/")+1,path.length()))
    }
}